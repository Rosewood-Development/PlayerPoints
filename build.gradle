import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java-library'
    id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'org.black_ixx'
version = '3.0.2'

repositories {
    mavenCentral()

    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://nexus.hc.to/content/repositories/pub_releases' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.dakanilabs.com/repository/public' }
    maven { url = 'https://repo.maven.apache.org/maven2' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    api 'dev.rosewood:rosegarden:1.0.7-SNAPSHOT'

    compileOnly 'org.spigotmc:spigot:1.16.5'
    compileOnly 'com.github.milkbowl:vault:1.7.3'
    compileOnly 'com.vexsoftware:votifier:2.7.2'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'org.jetbrains:annotations:16.0.2'
    compileOnly 'me.realized:tokenmanager:3.2.5'
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.encoding = 'UTF-8'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

shadowJar {
    archiveClassifier.set(null)
    minimize()

    relocate('dev.rosewood.rosegarden', 'org.black_ixx.playerpoints.libs.rosegarden')
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'playerpoints'
            pom {
                name = 'playerpoints'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar
