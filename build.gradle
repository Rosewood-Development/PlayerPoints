import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.gradleup.shadow' version '9.0.0-beta10'
    id 'java-library'
    id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'org.black_ixx'
version = '3.3.3-SNAPSHOT'

java {
    disableAutoTargetJvm()
}

repositories {
    mavenCentral()

    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://nexus.hc.to/content/repositories/pub_releases' }
    maven { url = 'https://repo.auxilor.io/repository/maven-public/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.maven.apache.org/maven2' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    api 'dev.rosewood:rosegarden:1.5.3'

    compileOnly 'org.spigotmc:spigot-api:1.21.7-R0.1-SNAPSHOT'
    compileOnly 'com.github.milkbowl:vault:1.7.3'
    compileOnly 'me.lokka30:treasury-api:1.2.1'
    compileOnly 'com.vexsoftware:votifier:2.7.2'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'org.jetbrains:annotations:24.0.1'
    compileOnly 'me.realized:tokenmanager:3.2.5'
    compileOnly 'su.nightexpress:gamepoints:1.3.1'
    compileOnly 'su.nexmedia:nexengine:2.2.0'
    compileOnly 'com.willfp:eco:6.75.2'
    compileOnly 'com.willfp:EcoBits:1.8.4'
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.encoding = 'UTF-8'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

shadowJar {
    archiveClassifier.set(null)
    minimize()

    relocate('dev.rosewood.rosegarden', 'org.black_ixx.playerpoints.libs.rosegarden')
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'playerpoints'
            pom {
                name = 'playerpoints'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar
generateMetadataFileForShadowPublication.dependsOn jar
